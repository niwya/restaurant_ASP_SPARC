% Constants used %
#const wnum=1. % Total number of waiters
#const cnum=2. % Total number of customers
#const tnum=2. % Total number of tables
#const maxcap=2. % Maximum capacity of tables
%----------------------------------------------------------------------------------------%
% Number of steps %
#const n=5.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
sorts 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Step range %%
#step = 0..n.
%----------------------------------------------------------------------------------------%
%% Population %%
#waiter=[w][1..wnum].
#customer=[c][1..cnum].
#person = #customer + #waiter.
#employee = #waiter + {agent}.
#moving_entity= #person + {agent}.
%----------------------------------------------------------------------------------------%
%% Environment %%
#room = {mainroom, kitchen, entrance}.
#table = [table][1..tnum].
#chair = [chair][1..maxcap][t][1..tnum].

#customerfurniture = #table + #chair.               %% Furniture that can be free/occupied
#otherfurniture = {counter, door}.                  %% "Inert" funiture 
#furniture = #customerfurniture + #otherfurniture.  %% All types of furniture
%----------------------------------------------------------------------------------------%
%% Paths %%
#node = {a,b,c,d,h,e,f}.
%----------------------------------------------------------------------------------------%
%% Entities %%
#entity = #moving_entity + #furniture + #node.
%----------------------------------------------------------------------------------------%
%% To manage pick and drop actions %%
#object=#chair.
#location=#table.
%----------------------------------------------------------------------------------------%
%% For observations %%
#boolean = {true,false}.
%----------------------------------------------------------------------------------------%
%% Fluents %%
% INERTIAL FLUENTS : can be directly/indirectly changed by actions %
#inertial_fluent=
                    isonchair(#customer(Cu),#chair(Ch))
                    + iswaiting(#customer(Cu))
                    + isattable(#chair(Ch),#table(T))
                    + isfree(#customerfurniture(Fr)) 
                    + isinroom(#entity(E),#room(R)) 
                    + holding(#object(O)) 
                    + haspaid(#customer(Cu)) 
                    + hasleft(#customer(Cu)) 
                    + currentlocation(#employee(E),#node(N)) 
                    + iswithagent(#customer(Cu))
% OBSERVATION-RELATED FLUENTS :
                    + bill_wave(#customer(Cu))                  % "Bring me the bill" sign
                    + wants_bill(#customer(Cu)).
#fluent = #inertial_fluent.
%----------------------------------------------------------------------------------------%
%% Actions %%
#actionseat = seat_customer(#customer(Cu), #chair(Ch)).
#actioncome = pick_customer(#customer(Cu)).
#actionpay = give_bill(#customer(Cu)).
#actiongo = go_to(#employee(E),#node(N)).
#actionpick = pick(#object(O),#location(L)).
#actiondrop = drop(#object(O),#location(L)).

#action = 
                    #actionseat                      % Agent assigns a customer to a chair
                    + #actioncome                    % Tells a customer to come with agent
                    + #actionpay                     % Agent gives bill to customer
                    + #actiongo                      % Agent moves to a predefined node
                    + #actionpick                    % Agent picks objet
                    + #actiondrop.                   % Agent drops object

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
predicates
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%% Paths %%
edge(#node,#node).
areassociated(#node,#furniture).
%----------------------------------------------------------------------------------------%
%% Goal %%
goal(#step).
success().
something_happened(#step).
%----------------------------------------------------------------------------------------%
%% Gestion of fluents and actions %%
holds(#fluent, #step).
occurs(#action, #step).
%----------------------------------------------------------------------------------------%
%% Gestion of observations %%
observed(#fluent, #boolean, #step).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
rules
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%  %%
%% Location of tables %%
% Tables cannot be moved %
holds(isinroom(table1, mainroom),I).
holds(isinroom(table2, mainroom),I).
holds(isinroom(counter, kitchen),I).
holds(isinroom(door, entrance),I).
%----------------------------------------------------------------------------------------%
%% Paths linking nodes %% 
% There is no other paths than the predefined one, paths may be blocked %
edge(a,b).
edge(b,a).  edge(b,c).
edge(c,b).  edge(c,d).
edge(d,c).  edge(d,e).  edge(d,h).
edge(e,d).  edge(e,f).
edge(f,e).
edge(h,d).
% Commutativity of the 'edge' predicate is not clearly stated as rule but no need %
%----------------------------------------------------------------------------------------%
%% Location of chairs at tables %%
% Chairs may be moved between tables %
holds(isattable(chair1t1, table1),0).
holds(isattable(chair2t1, table1),0).
holds(isattable(chair1t2, table2),0).
holds(isattable(chair2t2, table2),0).
%----------------------------------------------------------------------------------------%
%% Locations of customers %%
holds(iswaiting(c1),0).
holds(isonchair(c2,chair2t2),0).
%----------------------------------------------------------------------------------------%
%% Location of waiters and agent %%
holds(currentlocation(agent,e),0). 
holds(currentlocation(w1,a),0).
%----------------------------------------------------------------------------------------%

%% GOAL GESTION %%
%% Failure is not an option %%
success:- goal(I).
:- not success.
occurs(A,I) | -occurs(A,I) :- not goal(I).
%% Procrastinating is not an option %%
something_happened(I):- occurs(A,I).
:- goal(I), not goal(I-1), J<I, not something_happened(J).

%----------------------------------------------------------------------------------------%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GOALS TO BE SET BELOW: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%goal(I):- holds(haspaid(Cu),I).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%----------------------------------------------------------------------------------------%

%----------------------------------------------------------------------------------------%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSERVATIONS TO BE SET BELOW: %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%observed(bill_wave(c1), true, 3).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%----------------------------------------------------------------------------------------%

%% PERMANENT RULES %%
%% Inertia axiom %%
holds(F, I+1):- #inertial_fluent(F), holds(F,I), not -holds(F, I+1).
-holds(F, I+1):- #inertial_fluent(F), -holds(F, I), not holds(F, I+1).
%----------------------------------------------------------------------------------------%
%% Closed World Assumption for actions (if not known to occur, doesnt occur)  %%
-occurs(A,I):- not occurs(A,I).
%----------------------------------------------------------------------------------------%
%% Non-simultaneity of actions (the agent can only do 1 thing at a said step) %%
:- occurs(A1,I), occurs(A2,I), A1!=A2.
%----------------------------------------------------------------------------------------%
%% Reality check %%
:- observed(F,true,I), -holds(F,I).
:- observed(F,false,I), holds(F,I).
%----------------------------------------------------------------------------------------%
%% Full Awareness Axiom %%
holds(F,0) | -holds(F,0) :- #fluent(F).
%----------------------------------------------------------------------------------------%

%% CHAIRS %%
%% A chair is free unless stated otherwise %%
holds(isfree(Fr),I):- not -holds(isfree(Fr),I).
%----------------------------------------------------------------------------------------%
%% A chair is not free if a customer is sitting on it %%
-holds(isfree(Fr),I):- #chair(Fr), holds(isonchair(Cu,Fr),I).
holds(isfree(Fr),I+1):- #chair(Fr), holds(isonchair(Cu,Fr),I), occurs(seat_customer(Cu,Ch),I), Ch!=Fr.
%----------------------------------------------------------------------------------------%
%% Two customers cannot share the same chair at the same time %%
-holds(isonchair(Cu2,Ch),I):- holds(isonchair(Cu1,Ch),I), Cu1!=Cu2.
%----------------------------------------------------------------------------------------%
%% A customer cannot be on two chairs at the same time %% 
-holds(isonchair(Cu,Ch1),I):- holds(isonchair(Cu,Ch2),I), Ch1!=Ch2.
%----------------------------------------------------------------------------------------%
%% A chair cannot be at two tables at the same time %%
-holds(isattable(Ch,T2),I):- holds(isattable(Ch,T1),I), T2!=T1.
%----------------------------------------------------------------------------------------%
%% If customer Cu is not known to be on chair Ch at initial step, then he is not %%
-holds(isonchair(Cu, Ch), 0):- not holds(isonchair(Cu, Ch), 0).
%----------------------------------------------------------------------------------------%

%% TABLES %%
%% A table is not free if one of its associated chairs is not free %%
-holds(isfree(Fr),I):- #table(Fr), -holds(isfree(Ch),I), holds(isattable(Ch,Fr),I).
%----------------------------------------------------------------------------------------%

%% ROOMS %%
%% A customer who is waiting is in the entrance %%
holds(isinroom(Cu,entrance),I):- holds(iswaiting(Cu),I).
%----------------------------------------------------------------------------------------%
%% A customer, if seated, is in the room his chair is in %%
holds(isinroom(Cu,R),I):- holds(isonchair(Cu,Ch),I), holds(isinroom(Ch,R),I).
%----------------------------------------------------------------------------------------%
%% A chair in in the room its associated table is in %%
holds(isinroom(Ch,R),I):- holds(isattable(Ch,T),I), holds(isinroom(T,R),I).
%----------------------------------------------------------------------------------------%
%% An entity cannot be on multiple rooms at the same time %% 
-holds(isinroom(E,R1),I):- holds(isinroom(E,R2),I), R1!=R2.
%----------------------------------------------------------------------------------------%
%% If a customer is in the entrance at step I, is not anymore at step I+1 and has not been steated, then he has left %%
holds(hasleft(Cu),I+1):- holds(iswaiting(Cu),I), -holds(iswaiting(Cu),I+1), -holds(isonchair(Cu,Ch),I).
%----------------------------------------------------------------------------------------%
%% If something or someone is not known to be in room R at step, then it or he is not %%
-holds(isinroom(Cu,R),0):- not holds(isinroom(Cu,R),0). 
%----------------------------------------------------------------------------------------%
%% If a customer is not known to have left at step 0, then he has not %%
-holds(hasleft(Cu),0):- not holds(hasleft(Cu),0).
%----------------------------------------------------------------------------------------%
%% A client has left if he is not in any room anymore %% COMMENTED
%holds(hasleft(Cu),I):- -holds(isinroom(Cu,R),I).
%----------------------------------------------------------------------------------------%

%% PATHS %%
%----------------------------------------------------------------------------------------%
%% Two nodes that are not known to be associated to a piece of furniture are not %%
-areassociated(N,F):- not areassociated(N,F).
%----------------------------------------------------------------------------------------%
%% A node associated with a piece of furniture is in the room the furniture is %%
holds(isinroom(N,R),I):- #furniture(F), areassociated(N,F), holds(isinroom(F,R),I).
%% Others have to be declared manually %%
holds(isinroom(c,mainroom),I).
%----------------------------------------------------------------------------------------%
%% Node e is associated to door %%
areassociated(e,door).
%% Node f is associated to table1 %%
areassociated(f,table1).
%% Nodes f and h are associated to table2 %%
areassociated(f,table2).
%areassociated(h,table2).
%% Nodes a and b are associated to the counter %%
areassociated(a,counter).
areassociated(b,counter).
%----------------------------------------------------------------------------------------%
%% If there seems not to be an edge between nodes, then there is not %%
-edge(N1,N2):- not edge(N1,N2).
%----------------------------------------------------------------------------------------%
%% An employee can only be on one node at a time %%
-holds(currentlocation(E,N2),I):- holds(currentlocation(E,N1),I), N2!=N1.
%----------------------------------------------------------------------------------------%
%% Action go_to triggers a change in currentlocation %%
holds(currentlocation(E,N2),I+1):- occurs(go_to(E,N2),I), holds(currentlocation(E,N1),I), N2!=N1.
-holds(currentlocation(E,N1),I+1):- occurs(go_to(E,N2),I), holds(currentlocation(E,N1),I), N2!=N1.
%----------------------------------------------------------------------------------------%
%% If there is no edge between nodes, then go_to cannot occur %%
-occurs(go_to(E,N2),I):- -edge(N1,N2), holds(currentlocation(E,N1),I), N2!=N1.
%----------------------------------------------------------------------------------------%
%% An employee cannot go_to a location he is already at %%
-occurs(go_to(E,N),I):- holds(currentlocation(E,N),I).
%----------------------------------------------------------------------------------------%

%% PICKING UP AND SEATING %%
%% It is impossible to seat a customer on an occupied chair %%
-occurs(seat_customer(Cu1,Ch),I):- holds(isonchair(Cu2,Ch),I), Cu2!=Cu1.
%----------------------------------------------------------------------------------------%
%% If customer is seated on a chair at step I, he will be on it at step I+1 %%
holds(isonchair(Cu,Ch), I+1):- occurs(seat_customer(Cu,Ch), I).
%----------------------------------------------------------------------------------------%
%% It is impossible to seat a customer that is not with the agent %
-occurs(seat_customer(Cu,Ch),I):- -holds(iswithagent(Cu),I).
%----------------------------------------------------------------------------------------%
%% It is impossible for the agent to seat a customer if the agent is not on a node associated with the table the chair the customer will be seated on is at %
-occurs(seat_customer(Cu,Ch),I):- holds(currentlocation(N),I), holds(isattable(Ch,T),I), -areassociated(N,T).
%----------------------------------------------------------------------------------------%
%% A customer is not with the agent unless specified %%
-holds(iswithagent(Cu),I):- not holds(iswithagent(Cu),I).
%----------------------------------------------------------------------------------------%
%% The action of picking up a customer causes said customer, if he is waiting, not to be anymore %%
-holds(iswaiting(Cu),I+1):- occurs(pick_customer(Cu),I).
%----------------------------------------------------------------------------------------%
%% The action of picking up a customer causes said customer to be with agent instantly %%
holds(iswithagent(Cu),I):- occurs(pick_customer(Cu),I).
%----------------------------------------------------------------------------------------%
%% It is impossible to pick up a customer that is not waiting %%
-occurs(pick_customer(Cu),I):- -holds(iswaiting(Cu),I).
%----------------------------------------------------------------------------------------%
%% It is impossible to pick up a customer from another location than node e %%
-occurs(pick_customer(Cu),I):- -holds(currentlocation(E,e),I).
%----------------------------------------------------------------------------------------%

%% PAYMENT %%
%% Bringing the bill to a customer makes the haspaid fluent true for the following steps %%
holds(haspaid(Cu),I+1):- occurs(give_bill(Cu),I).
%----------------------------------------------------------------------------------------%
%% A customer has not paid, unless clearly stated %%
-holds(haspaid(Cu),I):- not holds(haspaid(Cu),I).
%----------------------------------------------------------------------------------------%
%% The agent cannot bring the bill to a customer if he is not on a node associated with the table the client is at %%
-occurs(give_bill(Cu),I):- holds(currentlocation(N),I), holds(isattable(Ch,T),I), -areassociated(N,T).
%----------------------------------------------------------------------------------------%
%% The agent cannot bring bill to a customer that is waiting %%
-occurs(give_bill(Cu),I):- holds(iswaiting(Cu),I).
%----------------------------------------------------------------------------------------%
%% The agent cannot give the bill to someone who has left %%
-occurs(give_bill(Cu),I):- holds(hasleft(Cu),I).
%----------------------------------------------------------------------------------------%
%% Observing a client waving for the bill generates give_bill goal %% NOT COMPLETE YET
%holds(wants_bill(Cu),I):- observed(bill_wave(Cu),true,I).
%----------------------------------------------------------------------------------------%



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% NOTES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% 1. As things are, a client may be brought a bill more than once during its stay. As  %%
%% it is coherent that it happens IRL, no rule has been added to block that.            %%
%% It is possible to avoid that by adding the rule:                                     %%
%% -occurs(give_bill(Cu),I):- occurs(give_bill(Cu),J), J<I.                             %%
%%                                                                                      %%
%% 2. Add the axiome de la flemme (minimizing the # of actions)                         %%
%%                                                                                      %%
%% 3. Only the go_to can be performed by any employee, add this feature on other        %%
%% actions too (not only the agent can do things)                                       %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%